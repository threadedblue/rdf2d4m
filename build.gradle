apply plugin: 'application'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'java-library'

applicationName = 'rdf2d4m'
sourceCompatibility = 1.8
mainClassName = 'iox.rdf2d4m.RDF2D4MDriver'
group = 'iox.d4m'
version = '0.0.1'


def jarFile = buildDir.name + "/libs/" + applicationName + "-" + version + ".jar"
configure(install.repositories.mavenInstaller) {
    pom.project {
        groupId group
        artifactId applicationName
        inceptionYear '2018'
        packaging 'jar'
    }
}

clean {
    delete "${buildDir}" 
    delete "${applicationName}" 
}

repositories {
    jcenter()
    maven {url 'file://' + new File(System.getProperty('user.home'), '.m2/repository').absolutePath;}
}
        
sourceSets {
    main {
        java {
            srcDirs 'src/main/java'
            srcDirs 'gen/main/java'
         }
    }
    test {
        java {
            srcDirs 'src/test/java'
         }
    }
}

dependencies {
    api "iox.d4m:accumulo.d4m:0.0.1"
    api "iox.accumulo:accumulo.access:0.0.1"
    api "args4j:args4j:2.33"
         
    // RDF4J
    api "org.openrdf.sesame:sesame-queryresultio-text:${rdfver}"

    // Logging
    api "org.apache.logging.log4j:log4j-slf4j-impl:${logver}" 
    
    api "org.apache.accumulo:accumulo-core:${accver}"
    api "org.apache.accumulo:accumulo-fate:${accver}"
    api "org.apache.accumulo:accumulo-start:${accver}"
    api "org.apache.accumulo:accumulo-trace:${accver}"
    api "org.apache.hadoop:hadoop-client:${hadver}"
    api "org.apache.zookeeper:zookeeper:${zoover}"
    api "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.9.5"
    api "com.fasterxml.jackson.core:jackson-databind:2.9.4"
    api "org.apache.commons:commons-lang3:3.4"
//    compile "com.google.guava:guava:14.0.1"
    api 'org.apache.commons:commons-math3:3.6.1'
    testImplementation 'junit:junit:4.12'
}

uploadArchives {
    repositories {
       flatDir {
           dirs 'repos'
       }
    }
}

println "lib=${configurations.runtimeClasspath}"

task bundle {
    dependsOn jar
    inputs.file jarFile
    outputs.dir applicationName
   doLast{ 
      copy {
            from "${buildDir}/libs"
            into "${applicationName}"
            fileMode = 0777
        }
       copy {
            from "conf/"
            into "${applicationName}/conf"
        }
       copy {
            from configurations.runtimeClasspath
            into "$applicationName/lib"
        }
   }
}
jar {
    // Keep jar clean:
    exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.MF'
    manifest {
        attributes ('Implementation-Title' : 'RDF 2 D4M',
                   'Implementation-Version': version,
                   'Main-Class': mainClassName,
                   'Class-Path': ' lib/' + configurations.runtimeClasspath.collect { it.getName() }.join(' lib/'))
        }
}
